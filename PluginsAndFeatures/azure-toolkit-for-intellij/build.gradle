buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
        }

    }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.11"
}

import org.apache.tools.ant.filters.*
processResources {
    filter(ReplaceTokens, tokens: ["applicationinsights.key": project.property("applicationinsights.key")])
}

group 'com.microsoft.azuretools'

compileJava {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

apply plugin: 'checkstyle'
apply plugin: 'findbugs'

checkstyle {
    toolVersion = '7.7'
    configFile = new File('config/checkstyle/google_checks.xml')
    showViolations = false
}

findbugs {
    toolVersion = "3.0.1"
    ignoreFailures = true
}

intellij {
    pluginName 'azure-toolkit-for-intellij'
    version intellij_version
    plugins = ['maven', dep_plugins]
	downloadSources Boolean.valueOf(sources)
}

sourceSets {
    main {
        java.srcDirs 'src'
        resources.srcDirs 'resources'
    }
    test {
        java {
            srcDir 'Test/java'
        }
        resources {
            srcDir 'Test/resources'
        }
    }
}

repositories {
    maven {url uri('../../.repository')} // to snap to the private maven repo on Jenkins if any
    mavenLocal()
    mavenCentral()
}

configurations {
    compile.exclude module:'slf4j-api'
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

apply plugin: 'java'

dependencies {
    compile fileTree(dir: '../AddLibrary/AzureLibraries/com.microsoft.azuretools.sdk/dependencies', include: ['applicationinsights-management-1.0.3.jar'])
    compile 'com.microsoft.azuretools:azuretools-core:3.0.4-SNAPSHOT'
    compile 'com.microsoft.azuretools:azure-explorer-common:3.0.4-SNAPSHOT'
    compile 'com.microsoft.azuretools:hdinsight-node-common:3.0.4-SNAPSHOT'
    testCompile 'junit:junit:4.12'
    testCompile 'info.cukes:cucumber-junit:1.2.5'
    testCompile 'info.cukes:cucumber-java:1.2.5'
    testCompile 'org.mockito:mockito-core:2.7.22'
    testCompile 'org.assertj:assertj-swing-junit:3.5.0'
    runtime 'com.microsoft.azure:azure-client-runtime:1.0.2'
    runtime 'com.microsoft.azure:azure-client-authentication:1.0.2'
}

task cucumberPackJar(type: Jar) {
    appendix = 'pathing'

    doFirst {
        manifest {
            attributes "Class-Path": configurations.cucumberRuntime.files.collect {
                it.toURL().toString().replaceFirst(/file:\/+/, '/')
            }.join(' ')
        }
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava, cucumberPackJar
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = files(sourceSets.main.output, sourceSets.test.output, cucumberPackJar.archivePath)
            args = [
                '--plugin', 'pretty',
                '--glue', 'com.microsoft.azure.hdinsight.spark.common',
                'Test/resources']
        }
    }
}

test.dependsOn cucumber

defaultTasks 'buildPlugin', 'test'
